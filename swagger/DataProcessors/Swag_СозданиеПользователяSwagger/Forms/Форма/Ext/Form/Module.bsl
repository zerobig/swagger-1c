#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяПользователя = нСтр("ru='swagger'");
	Пароль = нСтр("ru='swagger'");
	ОбновитьИнформацию();

КонецПроцедуры

&НаКлиенте
Процедура ИмяПользователяПриИзменении(Элемент)
	ИмяПользователяПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СоздатьПользователя(Команда)
	СоздатьПользователяНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СоздатьПользователяНаСервере()
	
	Если Не ЗначениеЗаполнено(ИмяПользователя) Тогда
		СтрокаСообщения = НСтр("ru = 'Не заполнено имя пользователя'");
		ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Пароль) Тогда
		СтрокаСообщения = НСтр("ru = 'Не заполнен пароль'");
		ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения);
	КонецЕсли;
	
	Пользователь = ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПользователя);
	Если Пользователь = Неопределено Тогда
		Пользователь = ПользователиИнформационнойБазы.СоздатьПользователя();
	Иначе
		СтрокаСообщения = НСтр("ru = 'Такой пользователь уже существует'");
		ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения);
		Возврат;
	КонецЕсли;
	
	Пользователь.АутентификацияСтандартная = Истина;
	Пользователь.Имя = ИмяПользователя;
	Пользователь.ПоказыватьВСпискеВыбора = Ложь;
	Пользователь.ПолноеИмя = ИмяПользователя;
	Пользователь.ЗапрещеноИзменятьПароль = Истина;
	Пользователь.Роли.Добавить(Метаданные.Роли.Swag_ОсновнаяРоль);
	Пользователь.Пароль = Пароль;
	Пользователь.Записать();
	СтрокаСообщения = НСтр("ru = 'Создан пользователь Swagger, перезапустите сеанс http сервиса'");
	ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения);
	ОбновитьИнформацию();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформацию()
	
	ТекстПараметровБазы = "";
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();
	
	ПараметрыБазы = СтроковыеФункцииКлиентСервер.ПараметрыИзСтроки(СтрокаСоединения);
	Если ПараметрыБазы.Свойство("Ref") Тогда
		ИмяБазы = ПараметрыБазы.Ref;
	Иначе
		СтрокаСообщения = НСтр("ru = 'База не серверная, невозможно определить публикацию'");
		ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяПользователя) Или Не ЗначениеЗаполнено(Пароль) Тогда
		СтрокаСообщения = НСтр("ru = 'Укажите имя пользователя и пароль'");
		ОбщегоНазначения.СообщитьПользователю(СтрокаСообщения); 
		Возврат;
	КонецЕсли;

	СтрокаСервисаSwagger = СтрШаблон(нСтр("ru='https://АдресСервера/%1/hs/swagger/index.html'"), ИмяБазы);
	СтрокиСервисаSwagger = СтрШаблон(нСтр("ru='%1%2%3?ui=redoc%4'"), СтрокаСервисаSwagger, Символы.ПС,СтрокаСервисаSwagger, Символы.ПС);
	Пользователь = ПользователиИнформационнойБазы.НайтиПоИмени(ИмяПользователя);
	
	Если Пользователь <> Неопределено Тогда
		ТекстПараметровБазы =СтрШаблон(нСтр("ru='Строка соединения сервиса Swagger:%1%2%3Логин: %4%5Пароль: %6'"), 
										Символы.ПС, СтрокиСервисаSwagger, Символы.ПС, ИмяПользователя, Символы.ПС, Пароль);
	Иначе
		ТекстПараметровБазы = нСтр("ru='Создайте пользователя Swagger'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИмяПользователяПриИзмененииНаСервере()
	ОбновитьИнформацию();
КонецПроцедуры

#КонецОбласти


