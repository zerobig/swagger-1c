Перем СтруктураОписания;

#Область ПрограммныйИнтерфейс

//
//
Функция Объект(ИмяОбъекта) Экспорт

	Если ПустаяСтрока(ИмяОбъекта) Тогда
		ВызватьИсключение "Объект: имя объекта не может быть пустым";
	КонецЕсли;
	
	Если СтруктураОписания = Неопределено Тогда
		СтруктураОписания = Новый Структура;
	КонецЕсли;
	
	СтруктураОписания.Вставить("ИмяОбъекта", ИмяОбъекта);
	
	ДобавитьПоследнийОбъектВМассивОбъектов();
	
	// Новый текущий объект
	СтруктураОписания.Вставить("ТекущийОбъект",
		Новый Структура("Имя, Свойства, ОбязательныеПоля, Описание", ИмяОбъекта));
	
	// Новый локальный массив свойств
	СтруктураОписания.Вставить("МассивСвойств", Новый Массив);
	
	// Новый массив обязательных полей
	СтруктураОписания.Вставить("ОбязательныеПоля", Неопределено);

	СтруктураОписания.Вставить("ОписаниеОбъекта", "");
	
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция ОбязательныеПоля(Поля) Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ИмяОбъекта") Или
		ПустаяСтрока(СтруктураОписания.ИмяОбъекта) Тогда
		
		ВызватьИсключение "ОбязательныеПоля: нельзя задавать обязательные поля для неопределенного объекта";
	КонецЕсли;
	Если Не ТипЗнч(Поля) = Тип("Строка") Тогда
		ВызватьИсключение "ОбязательныеПоля: обязательные поля должны быть переданы строкой, в которой перечислены наименования полей, разделенные запятыми";
	КонецЕсли;
	
	СтруктураОписания.Вставить("ОбязательныеПоля", Поля);

	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция Свойство(ИмяСвойства) Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ИмяОбъекта") Или
		ПустаяСтрока(СтруктураОписания.ИмяОбъекта) Тогда
		
		ВызватьИсключение "Свойство: нельзя задавать свойство объекта для неопределенного объекта";
	КонецЕсли;
	Если ПустаяСтрока(ИмяСвойства) Тогда
		ВызватьИсключение "Свойство: имя свойства объекта не может быть пустым";
	КонецЕсли;
	
	СтруктураОписания.Вставить("ТекущееСвойство", ИмяСвойства);
	
	МассивСвойств = Новый Массив;
	Если СтруктураОписания.Свойство("МассивСвойств") И ТипЗнч(СтруктураОписания.МассивСвойств) = Тип("Массив") Тогда
		МассивСвойств = СтруктураОписания.МассивСвойств;
	КонецЕсли;
	
	СтруктураНабораСвойств = Новый Структура("ИмяСвойства, Описание, Тип, Nullable,
			|Формат, Схема, Пример, Перечисление, Обязательный, Максимум, Минимум,
			|НеВключаяМаксимум, НеВключаяМинимум, МаксимальнаяДлина, МинимальнаяДлина,
			|ПоУмолчанию",
		ИмяСвойства, , , Ложь);
		
	// { Каратаев Олег - 01.08.2025
	СтруктураНабораСвойств.Вставить("ТипМассива");
	// }
		
	МассивСвойств.Добавить(СтруктураНабораСвойств);
	СтруктураОписания.Вставить("МассивСвойств", МассивСвойств);
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Устарела. Следует использовать ОписаниеСвойства
//
Функция Описание(СтрокаОписания) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "Описание: нельзя задавать описание для неопределенного свойства объекта";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Описание", СтрокаОписания);
	
	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция ТипЗначения(Тип) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "ТипЗначения: нельзя задавать тип значения для неопределенного свойства объекта";
	КонецЕсли;
	Если ПустаяСтрока(Тип) Тогда
		ВызватьИсключение "ТипЗначения: тип свойства не может быть пустым";
	КонецЕсли;
	Если Не НРег(Тип) = "number" И Не НРег(Тип) = "integer" И Не НРег(Тип) = "string" И
		Не НРег(Тип) = "boolean" И Не НРег(Тип) = "object" И Не НРег(Тип) = "array" Тогда
		
		ВызватьИсключение "ТипЗначения: тип свойства должен быть одним из следующего списка - number, integer, string, boolean, object или array";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Тип", Тип);
		
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция ДопускаетNull() Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "ДопускаетNull: нельзя задавать признак допущения null для неопределенного свойства объекта";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Nullable", Истина);
	
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция ФорматЗначения(СтрокаФормата) Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "Формат: нельзя задавать формат для неопределенного свойства объекта";
	КонецЕсли;
	
	// TODO: контроль в зависимости от типа
	
	УстановитьПараметрВМассивеСвойств("Формат", СтрокаФормата);
	
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция Обязательный() Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "Обязательный: нельзя задавать признак обязательности для неопределенного свойства объекта";
	КонецЕсли;

	УстановитьПараметрВМассивеСвойств("Обязательный", Истина);
	
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция Максимум(Значение) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "Максимум: нельзя задавать значение максимума для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "integer" И
		Не СвойствоТипЗначения.Тип = "number" Тогда
		
		ВызватьИсключение "Максимум: значение максимума можно задавать только для свойств с типом integer";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Максимум", Значение);
	
	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция Минимум(Значение) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "Минимум: нельзя задавать значение минимума для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "integer" И
		Не СвойствоТипЗначения.Тип = "number" Тогда
		
		ВызватьИсключение "Минимум: значение минимума можно задавать только для свойств с типом integer";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Минимум", Значение);

	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция НеВключаяМаксимум() Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "НеВключаяМаксимум: нельзя задавать условие включения максимального значения для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "integer" И
		Не СвойствоТипЗначения.Тип = "number" Тогда
		
		ВызватьИсключение "НеВключаяМаксимум: условие включения максимального значения можно задавать только для свойств с типом integer";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("НеВключаяМаксимум", Истина);
	
	Возврат ЭтотОбъект;

КонецФункции


//
//
Функция ТипМассива(Тип) Экспорт

	// { Андриянов Р.В. - 11.12.2024 - ITERP1C-2967
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "ТипМассива: нельзя задавать условие типа массива значения для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения.Тип = Неопределено И 
		Не СвойствоТипЗначения.Тип = "array" Тогда
		ВызватьИсключение "ТипМассива: условие включения массива можно задавать только для свойств с типом array";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("ТипМассива", Тип);
	
	Возврат ЭтотОбъект;
	// }

КонецФункции

//
//
Функция НеВключаяМинимум() Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "НеВключаяМинимум: нельзя задавать условие включения минимального значения для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "integer" И
		Не СвойствоТипЗначения.Тип = "number" Тогда
		
		ВызватьИсключение "НеВключаяМинимум: условие включения минимального значения можно задавать только для свойств с типом integer";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("НеВключаяМинимум", Истина);

	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция МаксимальнаяДлина(Значение) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "МаксимальнаяДлина: нельзя задавать значение максимальной длины для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "string" Тогда
		
		ВызватьИсключение "МаксимальнаяДлина: значение максимальной длины можно задавать только для свойств с типом string";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("МаксимальнаяДлина", Значение);
	
	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция МинимальнаяДлина(Значение) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "МинимальнаяДлина: нельзя задавать значение минимальной длины для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "string" Тогда
		
		ВызватьИсключение "МинимальнаяДлина: значение минимальной длины можно задавать только для свойств с типом string";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("МинимальнаяДлина", Значение);
	
	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция Схема(ТипОбъекта) Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "Схема: нельзя задавать схему для неопределенного свойства объекта";
	КонецЕсли;
	
	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "object" И
		Не СвойствоТипЗначения.Тип = "array" Тогда
		
		ВызватьИсключение "Схема: схему можно определять только для свойств с типом object или array";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Схема", ТипОбъекта);
		
	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция Пример(Пример) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "Пример: нельзя задавать пример для неопределенного свойства объекта";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Пример", Пример);
	
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция ЗначениеПеречисления(Значение) Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "ЗначениеПеречисления: нельзя задавать элементы перечисления для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "string" И
		Не СвойствоТипЗначения.Тип = "integer" Тогда
		
		ВызватьИсключение "ЗначениеПеречисления: перечисление можно устанавливать только для свойства типов string и integer";
	КонецЕсли;

	Перечислене = Новый Массив;
	Если СвойствоТипЗначения.Свойство("Перечисление") И ТипЗнч(СвойствоТипЗначения.Перечисление) = Тип("Массив") Тогда
		Перечислене = СвойствоТипЗначения.Перечисление;
	КонецЕсли;

	Перечислене.Добавить(Значение);
	УстановитьПараметрВМассивеСвойств("Перечисление", Перечислене);
	
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция Сформировать() Экспорт
	
	ДобавитьПоследнийОбъектВМассивОбъектов();
	
	МассивОписанийОбъектов = Новый Массив;
	Если СтруктураОписания.Свойство("МассивОбъектов") И
		ТипЗнч(СтруктураОписания.МассивОбъектов) = Тип("Массив") Тогда
		
		Для каждого Объект Из СтруктураОписания.МассивОбъектов Цикл
			ОписаниеОбъекта = Swag_ФормированиеОписаний.ОписаниеОбъекта(Объект.Имя,
				"object", Объект.Свойства, Объект.ОбязательныеПоля, Объект.Описание);
			МассивОписанийОбъектов.Добавить(ОписаниеОбъекта);
		КонецЦикла;
	КонецЕсли;
	
	Возврат МассивОписанийОбъектов;
	
КонецФункции

//
//
Функция ОписаниеОбъекта(Описание) Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ИмяОбъекта") Или
		ПустаяСтрока(СтруктураОписания.ИмяОбъекта) Тогда

		ВызватьИсключение "ОписаниеОбъекта: нельзя задавать описание для неопределенного объекта";
	КонецЕсли;

	СтруктураОписания.Вставить("ОписаниеОбъекта", Описание);
	
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция ОписаниеСвойства(СтрокаОписания) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "ОписаниеСвойства: нельзя задавать описание для неопределенного свойства объекта";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Описание", СтрокаОписания);
	
	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция ПоУмолчанию(Значение) Экспорт

	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "ПоУмолчанию: нельзя задавать значение по-умолчанию для неопределенного свойства объекта";
	КонецЕсли;

	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "string" И
		Не СвойствоТипЗначения.Тип = "integer" И
		Не СвойствоТипЗначения.Тип = "number" Тогда
		
		ВызватьИсключение "ПоУмолчанию: значение по-умолчанию можно задавать только для свойств с типом string, integer или number";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("ПоУмолчанию", Значение);
	
	Возврат ЭтотОбъект;

КонецФункции

//
//
Функция ОбъединениеСхем(СпособОбъединения) Экспорт
	
	Если СтруктураОписания = Неопределено Или
		Не СтруктураОписания.Свойство("ТекущееСвойство") Или
		ПустаяСтрока(СтруктураОписания.ТекущееСвойство) Тогда
		
		ВызватьИсключение "ОбъединениеСхем: нельзя устанавливать объединение схем для неопределенного свойства объекта";
	КонецЕсли;
	
	СвойствоТипЗначения = ПолучитьСвойствоВМассивеСвойств();
	Если Не СвойствоТипЗначения = Неопределено И
		Не СвойствоТипЗначения.Тип = Неопределено И
		Не СвойствоТипЗначения.Тип = "object" И
		Не СвойствоТипЗначения.Тип = "array" Тогда
		
		ВызватьИсключение "ОбъединениеСхем: объединение схем можно определять только для свойств с типом object или array";
	КонецЕсли;
	
	УстановитьПараметрВМассивеСвойств("Схема", Новый Структура("СпособОбъединенияСхем, МассивТиповСхемы",
		СпособОбъединения, Новый Массив));
		
	Возврат ЭтотОбъект;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьПараметрВМассивеСвойств(ИмяПараметра, Значение)
	
	Для каждого Свойство Из СтруктураОписания.МассивСвойств Цикл
		Если Свойство.ИмяСвойства = СтруктураОписания.ТекущееСвойство Тогда
			Если ИмяПараметра = "Схема" И ТипЗнч(Свойство["Схема"]) = Тип("Структура") Тогда
				МассивТипов = Свойство["Схема"].МассивТиповСхемы;
				МассивТипов.Добавить(Значение);
				Свойство["Схема"].МассивТиповСхемы = МассивТипов;
			Иначе
				Свойство[ИмяПараметра] = Значение;
			КонецЕсли;
			
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ВызватьИсключение СтрШаблон("УстановитьПараметрВМассивеСвойств: в массиве свойств не найдено свойство с именем ""%1""",
		СтруктураОписания.ТекущееСвойство);
	
КонецПроцедуры

Функция ПолучитьСвойствоВМассивеСвойств()
	
	Для каждого Свойство Из СтруктураОписания.МассивСвойств Цикл
		Если Свойство.ИмяСвойства = СтруктураОписания.ТекущееСвойство Тогда
			Возврат Свойство;
		КонецЕсли;
	КонецЦикла;
	
	ВызватьИсключение СтрШаблон("ПолучитьСвойствоВМассивеСвойств: в массиве свойств не найдено свойство с именем ""%1""",
		СтруктураОписания.ТекущееСвойство);
	
КонецФункции

Процедура ДобавитьПоследнийОбъектВМассивОбъектов()

	Если СтруктураОписания.Свойство("ТекущийОбъект") И
		ТипЗнч(СтруктураОписания.ТекущийОбъект) = Тип("Структура")
		И Не СтруктураОписания.ТекущийОбъект.Количество() = 0 Тогда
		
		ДобавитьСвойстваВОбъект(СтруктураОписания.ТекущийОбъект);
		СтруктураОписания.ТекущийОбъект.ОбязательныеПоля = СтруктураОписания.ОбязательныеПоля;
		СтруктураОписания.ТекущийОбъект.Описание         = СтруктураОписания.ОписаниеОбъекта;
		
		МассивОбъектов = Новый Массив;
		Если СтруктураОписания.Свойство("МассивОбъектов") И
			ТипЗнч(СтруктураОписания.МассивОбъектов) = Тип("Массив") Тогда
			
			МассивОбъектов = СтруктураОписания.МассивОбъектов;
		КонецЕсли;
		
		МассивОбъектов.Добавить(СтруктураОписания.ТекущийОбъект);
		СтруктураОписания.Вставить("МассивОбъектов", МассивОбъектов);
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьСвойстваВОбъект(ТекущийОбъект)
	
	МассивСвойствОбъекта = Новый Массив;

	Если СтруктураОписания.Свойство("МассивСвойств") И
		ТипЗнч(СтруктураОписания.МассивСвойств) = Тип("Массив") Тогда
		
		Для каждого СвойствоОбъекта Из СтруктураОписания.МассивСвойств Цикл
			
			Если ТипЗнч(СвойствоОбъекта.Схема) = Тип("Структура") Тогда
				СвойствоОбъекта.Схема = Swag_ФормированиеОписаний.ОписаниеСоставнойСхемы(
					СвойствоОбъекта.Схема.СпособОбъединенияСхем, СвойствоОбъекта.Схема.МассивТиповСхемы);
			КонецЕсли;
			
			СтруктураСвойства = Swag_ФормированиеОписаний.ОписаниеРеквизитаОбъекта(
				СвойствоОбъекта.ИмяСвойства, СвойствоОбъекта.Тип, СвойствоОбъекта.Схема);
			ЗаполнитьЗначенияСвойств(СтруктураСвойства, СвойствоОбъекта,, "Тип, Схема");
			
			МассивСвойствОбъекта.Добавить(СтруктураСвойства);
				
		КонецЦикла;
	
	КонецЕсли;
	
	ТекущийОбъект.Свойства = МассивСвойствОбъекта;
	
КонецПроцедуры

#КонецОбласти
